// setup.iss
; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "ViveStream"
#define MyAppPublisher "Md. Siam Mia"
#define MyAppURL "https://github.com/Md-Siam-Mia-Code/ViveStream-Revived"
#define MyAppExeName "vivestream.exe"

; These are passed in by build-installer.js
#ifndef MyAppVersion
#define MyAppVersion "1.9.0"
#endif

#ifndef SourceAppPath
#define SourceAppPath "release\vivestream-win32-ia32"
#endif

#ifndef AppIcon
#define AppIcon "assets\icon.ico"
#endif


[Setup]
AppId={{FC44F321-AE31-46B5-B6B2-BA2671A4C86C}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=ViveStream-Setup-v{#MyAppVersion}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
UninstallDisplayIcon={app}\{#MyAppExeName}
SetupIconFile={#AppIcon}
CloseApplications=yes
RestartApplications=no
UninstallLogMode=append

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#SourceAppPath}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallRun]
Filename: "{cmd}"; Parameters: "/C taskkill /f /im {#MyAppExeName}"; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

[Code]
var
  DeleteDataCheck: TNewCheckBox;

procedure InitializeUninstallProgressForm;
begin
  DeleteDataCheck := TNewCheckBox.Create(UninstallProgressForm);
  with DeleteDataCheck do
  begin
    Parent := UninstallProgressForm;
    Left := UninstallProgressForm.ProgressBar.Left;
    Top := UninstallProgressForm.ProgressBar.Top + UninstallProgressForm.ProgressBar.Height + 16;
    Width := UninstallProgressForm.ProgressBar.Width;
    Caption := 'Delete all downloaded media and settings';
    Checked := False;
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  DataPath: String;
begin
  if (CurUninstallStep = usPostUninstall) and (DeleteDataCheck.Checked) then
  begin
    DataPath := ExpandConstant('{userpf}\ViveStream');
    if DirExists(DataPath) then
    begin
      Log(Format('User checked option to delete data. Deleting: %s', [DataPath]));
      DelTree(DataPath, True, True, True);
    end
    else
    begin
      Log(Format('User checked option to delete data, but directory not found: %s', [DataPath]));
    end;
  end;
end;